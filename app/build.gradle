apply plugin: "com.android.application"

apply plugin: "kotlin-android"

apply plugin: "kotlin-android-extensions"

apply plugin: "kotlin-kapt"

def cmd = "git rev-list HEAD --count"
def gitVersion = cmd.execute().text.trim().toInteger()

static def getAlphaVersionName() {
    "alpha" + new Date().format("yyyyMMdd")
}

android {
    packagingOptions {
        exclude "lib/*/libRSSupport.so"
        exclude "lib/*/librsjni.so"
        exclude "lib/*/librsjni_androidx.so"
    }

    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "soko.ekibun.bangumi"
        minSdkVersion 22
        targetSdkVersion 28
        versionCode gitVersion
        versionName getAlphaVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "boolean", "AUTO_UPDATES", "true"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    compileOptions{
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    flavorDimensions "versionCode"
    productFlavors {
        coolapk {
            dimension "versionCode"
            buildConfigField "boolean", "AUTO_UPDATES", "false"
        }
        github {
            dimension "versionCode"
        }
    }
}

dependencies {
    def room_version = "2.2.3"
    def glide_version = "4.11.0"
    def okhttp_version = "4.5.0"

    implementation fileTree(include: ["*.jar"], dir: "libs")
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.core:core-ktx:1.1.0"
    implementation "androidx.preference:preference:1.1.0"
    testImplementation "junit:junit:4.12"
    androidTestImplementation "androidx.test:runner:1.2.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"
    implementation "com.google.android.material:material:1.2.0-alpha04"
    implementation "com.google.code.gson:gson:2.8.6"
    implementation "org.jsoup:jsoup:1.13.1"
    implementation "com.github.bumptech.glide:glide:${glide_version}"
    kapt "com.github.bumptech.glide:compiler:${glide_version}"
    implementation "com.zlc.glide:webpdecoder:1.7.${glide_version}"
    implementation "com.github.bumptech.glide:okhttp3-integration:${glide_version}"
    implementation "com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.3"
    implementation "com.squareup.okhttp3:okhttp:${okhttp_version}"
    implementation "com.squareup.okhttp3:okhttp-brotli:${okhttp_version}"
    implementation "com.squareup.okhttp3:logging-interceptor:${okhttp_version}"
    implementation "io.reactivex.rxjava2:rxjava:2.2.19"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "jp.wasabeef:glide-transformations:4.0.1"
    implementation "com.github.siyamed:android-shape-imageview:0.9.3"
    implementation "com.zhy:base-adapter:3.0.3"
    implementation "com.nshmura:recyclertablayout:1.5.0"
    implementation "com.github.chrisbanes:PhotoView:2.1.4"
    implementation "am.util:viewpager:25.3.0"
    implementation "com.jakewharton:disklrucache:2.0.2"
    implementation "com.caverock:androidsvg:1.2.1"
    implementation "com.umeng.umsdk:analytics:8.0.0"
    implementation "com.umeng.umsdk:common:2.0.0"
}
